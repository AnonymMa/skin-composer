plugins {
    id 'org.beryx.runtime' version '1.8.4'
}
apply plugin: "java"

sourceCompatibility = 11
sourceSets.main.java.srcDirs = [ "src/" ]

mainClassName = "com.ray3k.skincomposer.splash.Splash"
project.ext.assetsDir = new File("/assets")

def exeName = 'SkinComposerSplash'

task dist(type: Jar) {
    archiveFileName = "${appName}-splash.jar";
    from files(sourceSets.main.output.getClassesDirs())
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}
    from files(project.assetsDir)
    destinationDirectory = file("$buildDir/lib")
    manifest {
        attributes ('Main-Class': 'com.ray3k.skincomposer.splash.Splash', 'SplashScreen-Image' : 'splash.png')
    }
}

dist.dependsOn classes
jpackageImage.dependsOn dist

runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']
    modules = ['java.base' ,
               'java.desktop',
               'jdk.unsupported']
    distDir = file("$buildDir")
    jpackage {
        mainJar = "${appName}-splash.jar"
        imageOptions = ['--icon', file('../icons/logo.ico')]
        imageName = exeName
    }
}