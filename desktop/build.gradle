plugins {
    id 'org.beryx.runtime' version '1.8.4'
}
apply plugin: "java"

sourceCompatibility = 11
sourceSets.main.java.srcDirs = [ "src/" ]

def exeName = 'SkinComposer'

mainClassName = "com.ray3k.skincomposer.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../core/assets")

task dist(type: Jar) {
    archiveFileName = "${project.appName}.jar";
    from files(sourceSets.main.output.getClassesDirs())
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}
    from files(project.assetsDir)
    destinationDirectory = file("$buildDir/lib")
    manifest {
        attributes ('Main-Class': project.mainClassName)
    }
}

dist.dependsOn classes
jpackageImage.dependsOn dist

runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']
    modules = ['java.base' ,
               'java.desktop',
               'jdk.unsupported']
    distDir = file("$buildDir")
    jpackage {
        mainJar = "${appName}.jar"
        imageName = 'Skin Composer'
        installerName = 'Skin Composer'
        installerOptions = ['--linux-menu-group', 'Programming']
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions = ["--icon", file("../icons/logo.ico")]
        } else if (org.gradle.internal.os.OperatingSystem.current().linux) {
            imageOptions = ["--icon", file("../icons/logo.png")]
        } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            imageOptions = ["--icon", file("../icons/logo.icns")]
            jvmArgs = ['-XstartOnFirstThread']
        }
        imageName = exeName
    }
}